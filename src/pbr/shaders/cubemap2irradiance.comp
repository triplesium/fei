#version 450 core
layout(local_size_x=8, local_size_y=8, local_size_z=1) in;
uniform samplerCube cubemap;
layout(rgba32f) uniform writeonly imageCube output_texture;

const float PI = 3.14159265359;
const float TwoPI = 2 * PI;

vec3 getSamplingVector()
{
    vec2 st = gl_GlobalInvocationID.xy/vec2(imageSize(output_texture));
    vec2 uv = 2.0 * vec2(st.x, 1.0-st.y) - vec2(1.0);

    vec3 ret;
    switch (gl_GlobalInvocationID.z) {
        case 0: ret = vec3(1.0,  uv.y, -uv.x); break;
        case 1: ret = vec3(-1.0, uv.y,  uv.x); break;
        case 2: ret = vec3(uv.x, 1.0, -uv.y); break;
        case 3: ret = vec3(uv.x, -1.0, uv.y); break;
        case 4: ret = vec3(uv.x, uv.y, 1.0); break;
        case 5: ret = vec3(-uv.x, uv.y, -1.0); break;
    }
    return normalize(ret);
}

void main(void)
{
	vec3 N = getSamplingVector();

    vec3 irradiance = vec3(0.0);
    
    vec3 up = abs(N.y) > 0.999 ? vec3(1.0, 0.0, 0.0) : vec3(0.0, 1.0, 0.0);
    vec3 right = normalize(cross(up, N));
    up = normalize(cross(N, right));

    float sampleDelta = 0.02;
    float nrSamples = 0.0;
    for(float phi = 0.0; phi < TwoPI; phi += sampleDelta)
    {
        for(float theta = 0.0; theta < 0.5 * PI; theta += sampleDelta)
        {
            // spherical to cartesian (in tangent space)
            vec3 tangentSample = vec3(sin(theta) * cos(phi), sin(theta) * sin(phi), cos(theta));
            // tangent space to world
            vec3 sampleVec = tangentSample.x * right + tangentSample.y * up + tangentSample.z * N;
            vec3 sampled = texture(cubemap, sampleVec).rgb;
            float luminance = dot(sampled, vec3(0.2126, 0.7152, 0.0722));
            // filter out bright samples
            if (luminance > 50.0) {
                sampled = sampled * (50.0 / luminance);
            }
            irradiance += sampled * cos(theta) * sin(theta);
            nrSamples++;
        }
    }
    irradiance = PI * irradiance * (1.0 / float(nrSamples));
    imageStore(output_texture, ivec3(gl_GlobalInvocationID.xyz), vec4(irradiance, 1.0));
}
