#version 450 core
layout(local_size_x=32, local_size_y=32, local_size_z=1) in;
uniform sampler2D input_texture;
layout(rgba32f) uniform writeonly imageCube output_texture;

const float PI = 3.141592;
const float TwoPI = 2 * PI;

vec3 getSamplingVector()
{
    vec2 st = gl_GlobalInvocationID.xy/vec2(imageSize(output_texture));
    vec2 uv = 2.0 * vec2(st.x, 1.0-st.y) - vec2(1.0);

    vec3 ret;
    switch (gl_GlobalInvocationID.z) {
        case 0: ret = vec3(1.0,  uv.y, -uv.x); break;
        case 1: ret = vec3(-1.0, uv.y,  uv.x); break;
        case 2: ret = vec3(uv.x, 1.0, -uv.y); break;
        case 3: ret = vec3(uv.x, -1.0, uv.y); break;
        case 4: ret = vec3(uv.x, uv.y, 1.0); break;
        case 5: ret = vec3(-uv.x, uv.y, -1.0); break;
    }
    return normalize(ret);
}

void main(void)
{
	vec3 v = getSamplingVector();

	// Convert Cartesian direction vector to spherical coordinates.
	float phi   = atan(v.z, v.x);
	float theta = acos(v.y);

	// Sample equirectangular texture.
	vec4 color = texture(input_texture, vec2(phi/TwoPI, theta/PI));

	// Write out color to output cubemap.
	imageStore(output_texture, ivec3(gl_GlobalInvocationID), color);
}
